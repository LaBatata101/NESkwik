const std = @import("std");
const Controllers = @import("controller.zig").Controllers;
const CPU = @import("cpu.zig").CPU;
const Rom = @import("rom.zig").Rom;
const PPU = @import("ppu.zig").PPU;

const RAM: u16 = 0x0000;
const RAM_MIRRORS_END: u16 = 0x2000;
const PPU_REGISTERS: u16 = 0x2000;
const PPU_REGISTERS_MIRRORS_END: u16 = 0x4000;

//  _______________ $10000  _______________
// | PRG-ROM       |       |               |
// | Upper Bank    |       |               |
// |_ _ _ _ _ _ _ _| $C000 | PRG-ROM       |
// | PRG-ROM       |       |               |
// | Lower Bank    |       |               |
// |_______________| $8000 |_______________|
// | SRAM          |       | SRAM          |
// |_______________| $6000 |_______________|
// | Expansion ROM |       | Expansion ROM |
// |_______________| $4020 |_______________|
// | I/O Registers |       |               |
// |_ _ _ _ _ _ _ _| $4000 |               |
// | Mirrors       |       | I/O Registers |
// | $2000-$2007   |       |               |
// |_ _ _ _ _ _ _ _| $2008 |               |
// | I/O Registers |       |               |
// |_______________| $2000 |_______________|
// | Mirrors       |       |               |
// | $0000-$07FF   |       |               |
// |_ _ _ _ _ _ _ _| $0800 |               |
// | RAM           |       | RAM           |
// |_ _ _ _ _ _ _ _| $0200 |               |
// | Stack         |       |               |
// |_ _ _ _ _ _ _ _| $0100 |               |
// | Zero Page     |       |               |
// |_______________| $0000 |_______________|
pub const Bus = struct {
    /// The CPU has `0x0000..0x2000` addressing space reserved for RAM space. The RAM address
    /// space `0x000..0x0800` (2KiB) is mirrored three times:
    /// - `0x800..0x1000`
    /// - `0x1000..0x1800`
    /// - `0x1800..0x2000`
    /// This means that there is no difference in accessing memory addresses at `0x0000` or `0x0800` or
    /// `0x1000` or `0x1800` for reads or writes.
    ram: [2048]u8,
    rom: Rom,
    ppu: PPU,
    controllers: Controllers,

    dma_page: u8,
    dma_data: u8,
    dma_dummy: bool,
    dma_transfer: bool,

    const Self = @This();

    pub fn init(rom: Rom) Self {
        return .{
            .ram = [_]u8{0} ** 2048,
            .rom = rom,
            .ppu = PPU.init(rom.chr_rom, rom.mirroring),
            .controllers = Controllers.init(),
            .dma_transfer = false,
            .dma_dummy = true,
            .dma_page = 0,
            .dma_data = 0,
        };
    }

    pub fn mem_read(self: *Self, addr: u16) u8 {
        if (addr >= RAM and addr < RAM_MIRRORS_END) {
            const mirror_down_addr = addr & 0b00000111_11111111;
            return self.ram[mirror_down_addr];
        } else if (addr == 0x2000 or addr == 0x2001 or addr == 0x2003 or addr == 0x2005 or addr == 0x2006 or addr == 0x4014) {
            std.log.warn("Attempt to read from write-only PPU address {X:04}", .{addr});
            return 0;
        } else if (addr == 0x2002) {
            return @bitCast(self.ppu.status_read());
        } else if (addr == 0x2004) {
            return self.ppu.oam_data_read();
        } else if (addr == 0x2007) {
            return self.ppu.data_read();
        } else if (addr >= 0x2008 and addr < PPU_REGISTERS_MIRRORS_END) {
            const mirror_down_addr = addr & 0b00100000_00000111;
            return self.mem_read(mirror_down_addr);
        } else if (addr >= 0x4000 and addr <= 0x4015) {
            // TODO: implement APU
            return 0;
        } else if (addr == 0x4016) {
            return self.controllers.cntrl1_read();
        } else if (addr == 0x4017) {
            return self.controllers.cntrl2_read();
        } else if (addr >= 0x8000 and addr <= 0xFFFF) {
            return self.read_prg_rom(addr);
        } else {
            std.log.warn("Ignoring mem read at 0x{X:04}", .{addr});
            return 0;
        }
    }

    pub fn mem_write(self: *Self, addr: u16, data: u8) void {
        if (addr >= RAM and addr < RAM_MIRRORS_END) {
            const mirror_down_addr = addr & 0x07FF;
            self.ram[mirror_down_addr] = data;
        } else if (addr == 0x2000) {
            self.ppu.ctrl_write(data);
        } else if (addr == 0x2001) {
            self.ppu.mask_write(data);
        } else if (addr == 0x2003) {
            self.ppu.oam_addr_write(data);
        } else if (addr == 0x2004) {
            self.ppu.oam_data_write(data);
        } else if (addr == 0x2005) {
            self.ppu.scroll_write(data);
        } else if (addr == 0x2006) {
            self.ppu.addr_write(data);
        } else if (addr == 0x2007) {
            self.ppu.data_write(data);
        } else if (addr >= 0x2008 and addr < PPU_REGISTERS_MIRRORS_END) {
            const mirror_down_addr = addr & 0b00100000_00000111;
            self.mem_write(mirror_down_addr, data);
        } else if (addr >= 0x4000 and addr <= 0x4013 or addr == 0x4015) {
            // TODO: implement APU
        } else if (addr == 0x4014) {
            // https://www.nesdev.org/wiki/PPU_programmer_reference#OAMDMA_-_Sprite_DMA_($4014_write)
            self.dma_page = data;
            self.dma_transfer = true;
            self.ppu.oam_dma_addr = 0;
        } else if (addr >= 0x4016 and addr <= 0x4017) {
            self.controllers.set_strobe(data);
        } else if (addr >= 0x8000 and addr <= 0xFFFF) {
            @panic("Attempt to write to cartridge ROM memory space");
        } else {
            std.log.warn("Ignoring mem write at 0x{X:04}", .{addr});
        }
    }

    pub fn mem_read_u16(self: *Self, addr: u16) u16 {
        const lo = self.mem_read(addr);
        const hi = @as(u16, self.mem_read(addr + 1));
        return (hi << 8) | lo;
    }

    pub fn mem_write_u16(self: *Self, addr: u16, data: u16) void {
        const hi: u8 = @truncate(data >> 8);
        const lo: u8 = @truncate(data);
        self.mem_write(addr, lo);
        self.mem_write(addr + 1, hi);
    }

    // PRG Rom Size might be 16 KiB or 32 KiB. Because `0x8000..0x10000` mapped region is 32 KiB of addressable space,
    // the upper 16 KiB needs to be mapped to the lower 16 KiB (if a game has only 16 KiB of PRG ROM)
    fn read_prg_rom(self: Self, addr: u16) u8 {
        var new_addr = addr - 0x8000;
        // Check if the PRG Rom Size is 16 KiB and we're trying to access memory pass the 16 KiB.
        if (self.rom.prg_rom.len == 0x4000 and new_addr >= 0x4000) {
            new_addr = new_addr % 0x4000;
        }
        return self.rom.prg_rom[new_addr];
    }

    pub fn handle_dma_transfer(self: *Self, clock_counter: usize) bool {
        if (self.dma_transfer) {
            if (self.dma_dummy) {
                // waiting to synchronise the CPU to the DMA
                if (clock_counter % 2 == 1) self.dma_dummy = false;
            } else if (clock_counter % 2 == 0) {
                // on even cycles, read data from the CPU address space
                self.dma_data = self.mem_read(@as(u16, self.dma_page) << 8 | self.ppu.oam_dma_addr);
            } else {
                // on odd cycles, write data to the PPU's OAM
                self.ppu.oam_data_register[self.ppu.oam_dma_addr] = self.dma_data;
                self.ppu.oam_dma_addr +%= 1;
                // address wapped to 0; DMA transfer is over
                if (self.ppu.oam_dma_addr == 0) {
                    self.dma_dummy = true;
                    self.dma_transfer = false;
                }
            }
            return true;
        } else {
            return false;
        }
    }
};
